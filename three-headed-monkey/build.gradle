buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.8.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.2'
        classpath 'com.github.jcandksolutions.gradle:android-unit-test:1.0.+'
    }
}

apply plugin: 'android'
apply plugin: 'android-apt'

def getVersionCode = { ->
    def code = project.hasProperty('versionCode') ? versionCode.toInteger() : -1
    println "VersionCode is set to $code"
    return code
}

def getVersionName = { ->
    def code = getVersionCode
    def versionName = "0.1.$code"
    println "VersionName is set to $versionName"
    return versionName
}

android {
    compileSdkVersion 18
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 18
        packageName "net.three_headed_monkey"
        versionCode getVersionCode()
        versionName getVersionName()
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }
    }
}

apply plugin: 'android-unit-test'

repositories {
    mavenCentral()
}

ext.androidAnnotationsVersion = '2.7.1';


dependencies {
//==================== Android Annotations =========
    apt "com.googlecode.androidannotations:androidannotations:${androidAnnotationsVersion}"
    compile "com.googlecode.androidannotations:androidannotations-api:${androidAnnotationsVersion}"
//===================================================
    compile "com.google.code.gson:gson:2.2.4"
    compile "com.android.support:support-v4:18.0.+"
//==================== Robolectric ==================
    testCompile 'junit:junit:4.10'
    testCompile 'org.robolectric:robolectric:2.1.+'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'com.squareup:fest-android:1.0.+'
//==================== So android studio sees these! =
//    instrumentTestCompile 'junit:junit:4.10'
//    instrumentTestCompile'org.robolectric:robolectric:2.1.+'
//    instrumentTestCompile 'org.hamcrest:hamcrest-all:1.3'
//    instrumentTestCompile 'com.squareup:fest-android:1.0.+'
    instrumentTestCompile 'com.jayway.android.robotium:robotium-solo:4.3.+'
}

apt {
    arguments {
        androidManifestFile variant.processResources.manifestFile 
        resourcePackageName android.defaultConfig.packageName
        // You can set optional annotation processing options here, like these commented options:
        // logLevel 'INFO'
        // logFile '/var/log/aa.log'
    }
}

task jenkins() << {
    android.buildTypes.each { type ->
        type.packageNameSuffix = ".jenkins"
        type.versionNameSuffix = "-jenkins-$type.name"
    }
}

task createFlashableZip(type: Zip) {
    from "build-files/flashable-zip"
    from("build/apk/three-headed-monkey-release-unsigned.apk") {
        into "system/app"
    }
    baseName "three-headed-monkey-release"
}


